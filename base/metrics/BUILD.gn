# Copyright (c) 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# TODO(phosek) bug 570839: If field_trial.cc is in a static library,
# hacl_helper_nonsfi doesn't link properly on Linux in debug builds. The
# reasons for this seem to involve obscure toolchain bugs. This should be fixed
# and this target should be a static_library unconditionally.
import("//build/config/nacl/config.gni")
if (is_nacl_nonsfi) {
  metrics_target_type = "source_set"
} else {
  metrics_target_type = "static_library"
}

# Should be static library, see documentation on //base:base for discussion.
#static_library("metrics") {  # Should be this when above TODO is resolved.
target(metrics_target_type, "metrics") {
  sources = [
    "bucket_ranges.cc",
    "bucket_ranges.h",
    "field_trial.cc",
    "field_trial.h",
    "histogram.cc",
    "histogram.h",
    "histogram_base.cc",
    "histogram_base.h",
    "histogram_delta_serialization.cc",
    "histogram_delta_serialization.h",
    "histogram_flattener.h",
    "histogram_macros.h",
    "histogram_samples.cc",
    "histogram_samples.h",
    "histogram_snapshot_manager.cc",
    "histogram_snapshot_manager.h",
    "metrics_hashes.cc",
    "metrics_hashes.h",
    "sample_map.cc",
    "sample_map.h",
    "sample_vector.cc",
    "sample_vector.h",
    "sparse_histogram.cc",
    "sparse_histogram.h",
    "statistics_recorder.cc",
    "statistics_recorder.h",
    "user_metrics.cc",
    "user_metrics.h",
    "user_metrics_action.h",
  ]

  configs += [ "//base:base_implementation" ]

  deps = [
    "//base/debug",
    "//base/json",
    "//base/memory",
    "//base/process",
  ]

  allow_circular_includes_from = [
    "//base/memory",
    "//base/process",
  ]

  visibility = [ "//base/*" ]
}
